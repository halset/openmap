dnl Process this file with autoconf to produce a configure script.
AC_REVISION(OpenMap $Revision: 1.20 $)
AC_INIT(openmap.mk.in)

#
## Override default of /usr/local
#
AC_PREFIX_DEFAULT(/usr/local/openmap)

#
## Determine what paths to check to find the JDK.
## There are three possible cases:
## 1. The user used the --without-jdk command line flag.
##
##   In this case, error.  We can't build without a JDK.
##
## 2. The user either didn't use a command line flag or she
##    specified --with-jdk, but didn't specify a directory
##
##   In this case, use $PATH and add some possible directories on to
##   the end.
##
## 3. The user specified a directory using --with-jdk=/some/directory
##
##   In this case, try the specified directory and then the specified
##   directory with '/bin' appended.
##
#
JDK_PATH=""
case $with_jdk in
  no)
    # user specified --without-jdk
    AC_MSG_ERROR([Can't compile OpenMap without a JDK])
    ;;
  "" | yes)
    # user specified --with-jdk, but didn't specify where to find it
    JDK_PATH=$PATH:/usr/local/java/bin:/usr/local/jdk/bin:/usr/local/java/jdk/bin
    ;;
  *)
    # user specified a path, either relative or not
    JDK_PATH=$with_jdk:$with_jdk/bin
    ;;
esac

dnl AC_DIE_IF_NOT_FOUND(VARIABLE,name,PATH)
dnl isses an error and dies if VARIABLE is not set
AC_DEFUN(AC_DIE_IF_NOT_FOUND,
[if test -z "[$]$1"; then
  if test -z "$3"; then
    AC_MSG_ERROR([Can't build openmap without $2])
  else
    AC_MSG_ERROR([Can't locate $2 in $3])
  fi
fi
])


dnl Checks for programs.

#
## Handle the possibility that we're on a Cygwin platform.
##
## On the cygwin platform the CLASSPATH separator is a semi-colon
## instead of a colon.  The variable CLASSPATH_SEP is set to the
## appropriate separator.
##
#
CLASSPATH_SEP=":"
AC_SUBST(CLASSPATH_SEP)

AC_MSG_CHECKING([for cygwin platform])
if uname -s 2>/dev/null | grep -i cyg >/dev/null 2>&1; then
  AC_MSG_RESULT([yes])
  CLASSPATH_SEP=";"
else
  AC_MSG_RESULT([no])
fi

AC_PROG_INSTALL

AC_PATH_PROG(JAVA,java,,$JDK_PATH)
AC_DIE_IF_NOT_FOUND(JAVA,java,$JDK_PATH)

AC_PATH_PROG(JAVAC,javac,,$JDK_PATH)
AC_DIE_IF_NOT_FOUND(JAVAC,javac,$JDK_PATH)

AC_PATH_PROG(JAR,jar,,$JDK_PATH)
AC_DIE_IF_NOT_FOUND(JAR,jar,$JDK_PATH)

AC_PATH_PROG(JAVADOC,javadoc,,$JDK_PATH)
AC_DIE_IF_NOT_FOUND(JAVADOC,javadoc,$JDK_PATH)

AC_FIND_SWING_JAR


dnl Checks for libraries.

dnl Checks for header files.

dnl Checks for typedefs, structures, and compiler characteristics.

dnl Checks for library functions.

dnl Checks for packages
AC_ARG_WITH(jdk, [  --with-jdk=JDK_HOME     Home directory of JDK])
AC_ARG_WITH(swing, [  --with-swing=JARFILE    Swing jar file])

DEFAULT_TARGET=all
AC_SUBST(DEFAULT_TARGET)
case $with_devtarget in
  "")
    # nothing specified, use default as already set.
    ;;
  no)
    # user specified --without-devtarget.  Use default as already set.
    ;;
  yes)
    # user specified --with-devtarget, but didn't specify which target
    DEFAULT_TARGET=omcore
    ;;
  *)
    # user specified a target with --with-devtarget=target.  Use it.
    DEFAULT_TARGET=$with_devtarget
    ;;
esac


AC_OUTPUT(openmap.mk
	Makefile
	bin/openmap
	com/Makefile
	com/bbn/Makefile
	com/bbn/openmap/Makefile
	com/bbn/openmap/app/Makefile
	com/bbn/openmap/event/Makefile
	com/bbn/openmap/examples/Makefile
	com/bbn/openmap/examples/hello/Makefile
	com/bbn/openmap/examples/hello/hello-world
	com/bbn/openmap/gui/Makefile
	com/bbn/openmap/omGraphics/Makefile
	com/bbn/openmap/proj/Makefile
	com/bbn/openmap/util/Makefile
	com/bbn/openmap/layer/Makefile
	com/bbn/openmap/layer/dted/Makefile
	com/bbn/openmap/layer/nitf/Makefile
	com/bbn/openmap/layer/plotLayer/Makefile
	com/bbn/openmap/layer/pluginLayer/Makefile
	com/bbn/openmap/layer/rpf/Makefile
	com/bbn/openmap/layer/shape/Makefile
	com/bbn/openmap/layer/terrain/Makefile
	com/bbn/openmap/layer/util/Makefile
	com/bbn/openmap/layer/util/cacheHandler/Makefile
	com/bbn/openmap/layer/util/html/Makefile
	com/bbn/openmap/layer/util/http/Makefile
	com/bbn/openmap/layer/util/stateMachine/Makefile
	com/bbn/openmap/layer/vpf/Makefile
	)

AC_OUTPUT_COMMANDS([echo "chmod +x bin/openmap"],[chmod +x bin/openmap])

AC_OUTPUT_COMMANDS([echo "chmod +x com/bbn/openmap/examples/hello/hello-world"],[chmod +x com/bbn/openmap/examples/hello/hello-world])
